plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
}

import groovy.json.JsonSlurper

def examplesJsonFile = file("../examples.json")
def examplesJson = new JsonSlurper().parse(examplesJsonFile)
def projects = ["game"] + (examplesJson.examples ?: [])

android {
    namespace = 'org.gzp.shine'
    compileSdk = 36

    defaultConfig {
        applicationId = "org.gzp.shine"
        minSdk = 30
        targetSdk = 36
        versionCode = 1
        versionName = '1.0'
        externalNativeBuild {
            cmake {
                arguments = ['-DANDROID_STL=c++_shared']
            }
        }
        ndk {
            //noinspection ChromeOsAbiSupport
            abiFilters = ['arm64-v8a']
        }
        // testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    externalNativeBuild {
        cmake {
            path = 'CMakeLists.txt'
        }
    }
    buildTypes {
        release {
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            minifyEnabled = true
            signingConfig signingConfigs.debug
            //minifyEnabled = false
            //zipAlignEnabled = true
            //shrinkResources = true
        }

        debug {
            //minifyEnabled = false
            //shrinkResources = false
        }
    }
    flavorDimensions += 'project'
    productFlavors {
        projects.each { flavorName ->
            "$flavorName" {
                dimension = 'project'
                applicationIdSuffix = ".${flavorName}"
                buildConfigField "boolean", "REQUIRES_AUTHENTICATION", "${flavorName == 'game' ? 'true' : 'false'}"
            }
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
    buildFeatures {
        prefab = true
        buildConfig = true
    }
    packagingOptions {
        jniLibs {
            // This is a dummy library to get gradle to include libc++_shared.so
            excludes += ['lib/*/libgameActivity.so']
        }
    }
    sourceSets {
        main {            
            assets.srcDirs += files('../../assets')
            res.srcDirs += files('../../assets/android-res')
            jniLibs.srcDirs = ["src/main/jniLibs"]
        }

        projects.each { flavorName ->
            "$flavorName" {
                jniLibs.srcDirs += ["src/main/featureLibs/${flavorName}"]
            }
        }
    }
}

// Helper function to determine current flavors from project property or task name
def getCurrentFlavors() {
    def currentFlavors = []

    // First check if flavor is specified as project property (`-Pflavor=curve` or `-Pflavor=curve,asset`)
    if (project.hasProperty('flavor')) {
        def flavorProperty = project.flavor
        if (flavorProperty.contains(',')) {
            currentFlavors += flavorProperty.split(',').collect { it.trim() }
        } else {
            currentFlavors += [flavorProperty.trim()]
        }
    } else {
        // Fall back to detecting from task name (e.g., assembleCurveDebug -> curve)
        def taskName = gradle.startParameter.taskNames.find { it.contains('assemble') || it.contains('bundle') }

        if (taskName) {
            android.productFlavors.each { flavor ->
                if (taskName.toLowerCase().contains(flavor.name.toLowerCase())) {
                    currentFlavors += [flavor.name]
                }
            }
        }
    }

    if (currentFlavors.isEmpty()) {
        currentFlavors += android.productFlavors.collect { it.name }
    }

    return currentFlavors
}

// Register rust build tasks for all the selected flavors
getCurrentFlavors().each { flavorName ->
    android.defaultConfig.ndk.abiFilters.each { abi ->
        tasks.register("buildRustLib_${flavorName}[${abi}]", Exec) {
            group = 'rust'
            description = "Build Rust library for ${flavorName} flavor for ${abi} ABI"
            workingDir = project.projectDir.parent
          
            def outputDir = "android/src/main/featureLibs/${flavorName}"
            def cargoCommand = [
                'cargo', 'ndk',
                '-t', abi,
                '-o', outputDir,
                'build',
                '-p', 'shine-client',
                '--release',
                '--lib'
            ]

            if (flavorName != 'game') {
                cargoCommand += ['--features', "example_${flavorName}"]
            }

            commandLine cargoCommand

            doFirst {
                println "Building Rust library for flavor: ${flavorName}"
                println "Running: ${cargoCommand.join(' ')}"
            }
        }
    }
}


// Main task that depends on the relevant flavor subtasks
tasks.register("buildRustLib") {
    group = 'build'
    description = 'Build Rust library for the current flavors'
    dependsOn getCurrentFlavors().collect { flavor -> android.defaultConfig.ndk.abiFilters.collect { abi -> tasks.named("buildRustLib_${flavor}[${abi}]")  } }    
}

dependencies {
    implementation libs.androidx.appcompat
    implementation libs.androidx.games.activity
    implementation libs.androidx.browser
    implementation libs.androidx.security
    implementation libs.material
    implementation libs.okhttp
    implementation libs.androidx.core.ktx
    testImplementation libs.junit
    androidTestImplementation libs.ext.junit
    androidTestImplementation libs.androidx.espresso.core
}


def currentFlavors = getCurrentFlavors()
println "Current flavors: ${currentFlavors.join(', ')}"

tasks.matching { task ->
    task.name.startsWith('assemble') || task.name.startsWith('bundle')
}.configureEach { task ->
    task.dependsOn buildRustLib
}
